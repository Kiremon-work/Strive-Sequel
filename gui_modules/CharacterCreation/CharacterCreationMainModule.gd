extends Panel

var person
var mode
var total_stat_points
var unassigned_points

var preservedsettings = {}
var valid_preservedsettings = {}

var agearray = ['teen','adult','mature']
var sexarray = ['male','female','futa']
var malesizes = ['masculine','flat']
var sizes = ['flat','small','average','big','huge']
var short_sizes = ['small','average','big']
var guild = 'none'

var bodypartsarray = ['skin', 'hair_length', 'hair_color', 'eye_color', 'eye_shape', 'ears', 'horns', 'tail', 'wings', 'height']
var sexbodypartsarray = ['slave_class','penis_size', 'penis_type', 'balls_size','tits_size', 'ass_size']

var slave_classes = ['slave','servant']
var selected_class = ''

var introduction_text = {master = "Create your Master Character", 'slave' : 'Create your Starting Slave'}

var savefilename
var saveloadstate

var params_to_save = [
	"slave_class",
	"name",
	"surname",
	"nickname",
	"sex",
	"age",
	"race",
	"traits",
	"sex_traits",
	"personality",
	"skin",
	"height",
	"hair_length",
	"hair_color",
	"hair_style",
	"ears",
	"eye_color",
	"eye_shape",
	"horns",
	"wings",
	"tail",
	"arms",
	"legs",
	"body_lower",
	"body_shape",
	"penis_size",
	"penis_type",
	"balls_size",
	"tits_size",
#	"vagina",
	"ass_size",
#	"has_pussy",
	"multiple_tits",
	"multiple_tits_developed",
#	"has_womb",
	"penis_virgin",
	"vaginal_virgin",
	"anal_virgin",
	"mouth_virgin",
	"masternoun",
#	"food_filter",
	"food_hate",
	"food_love",
	"physics_factor",
	"magic_factor",
	"tame_factor",
	"timid_factor",
	"growth_factor",
	"charm_factor",
	"wits_factor",
	"sexuals_factor",
	"professions",
	"type"
]

onready var RaceSelection = $RaceSelectionModule
onready var ClassSelection = $ClassSelectionModule
onready var SlaveInfo = $SlaveCreationModule
onready var TraitSelection = $TraitSelection

func _ready():
	$SaveButton.connect("pressed", self, "SaveLoadCharPanel", ["save"])
	$LoadButton.connect("pressed", self, "SaveLoadCharPanel", ["load"])
	$SaveLoadCharPanel/LineEdit.connect("text_changed",self,'set_savefilename')
	$SaveLoadCharPanel/SaveLoadButton.connect("pressed", self, "PressSaveLoadCharacter", [savefilename])
	$SaveLoadCharPanel/SaveLoadCancel.connect("pressed", self, "hideSaveLoadPanel")
	# input_handler.AddPanelOpenCloseAnimation($RaceSelection)
	# input_handler.AddPanelOpenCloseAnimation($TraitSelection)
	# input_handler.AddPanelOpenCloseAnimation($DietPanel)
	# input_handler.AddPanelOpenCloseAnimation($ClassPanel)
	$TestButton.connect("pressed", self, "test")
	for i in agearray:
		$VBoxContainer/HBoxContainer/AgeVBox/age.add_item(i.capitalize())
	for i in sexarray:
		if input_handler.globalsettings.futa == false && i == 'futa':
			continue
		$VBoxContainer/HBoxContainer/SexVBox/sex.add_item(i.capitalize())
	$VBoxContainer/race.connect("pressed", RaceSelection, "select_race")
	$VBoxContainer/HBoxContainer/AgeVBox/age.connect("item_selected", self, "select_age")
	$VBoxContainer/HBoxContainer/SexVBox/sex.connect("item_selected", self, "select_sex")
	$VBoxContainer/sextrait.connect('pressed', self, "open_sex_traits")
	$VBoxContainer/trait.connect('pressed', self, "open_traits")


	$ConfirmButton.connect("pressed", self, 'confirm_character')
#	$CancelButton.connect("pressed", self, "confirm_return")
	globals.connecttexttooltip($VBoxContainer/sextrait, tr("TOOLTIPSEXTRAITS"))

	for i in ['name','surname','nickname']:
		$VBoxContainer.get_node(i).connect("text_changed", self, 'text_changed', [i])


	$VBoxContainer/class.connect("pressed", ClassSelection, "open_class_list")
	for i in $DietPanel/VBoxContainer.get_children():
		i.get_node("OptionButton").connect("item_selected", self, "select_food_pref", [i.name])
	$BackButton.connect("pressed", self, "Exit")
	$BackButtonCheats.connect("pressed", self, "hide")
	open()

func Exit():
	input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'MainMenu', tr('LEAVECONFIRM')])


func MainMenu():
	globals.return_to_main_menu()

func select_age(value):
	person.set_stat('age', agearray[value])
	preservedsettings['age'] = agearray[value]
	valid_preservedsettings['age'] = true
	rebuild_slave()

func select_sex(value):
	person.set_stat('sex', sexarray[value])
	preservedsettings['sex'] = sexarray[value]
	valid_preservedsettings['sex'] = true
	if preservedsettings.has("tits_size"):
		valid_preservedsettings['tits_size'] = false
#		preservedsettings.erase("tits_size")
	if preservedsettings.has("has_pussy"):
		valid_preservedsettings['has_pussy'] = false
#		preservedsettings.erase("has_pussy")
	if preservedsettings.has("has_womb"):
		valid_preservedsettings['has_womb'] = false
#		preservedsettings.erase("has_womb")
	if preservedsettings.has("vagina"):
		valid_preservedsettings['vagina'] = false
#		preservedsettings.erase("vagina")
	rebuild_slave()


func select_food_pref(selected_id, type):
	var food_hate = person.food.food_hate
	match selected_id:
		0:
			if person.food.food_love == type:
				person.food.food_love = ""
			if food_hate.has(type):
				food_hate.erase(type)
		1:
			person.food.food_love = type
			if food_hate.has(type):
				food_hate.erase(type)
		2:
			if !food_hate.has(type):
				food_hate.append(type)
			if person.food.food_love == type:
				person.food.food_love = ""
	select_diet()


func select_diet():
	var array = ['Neutral', "Like", "Hate"]
	for i in $DietPanel/VBoxContainer.get_children():
		i.get_node("OptionButton").set_item_disabled(1, person.food.food_love != '')
		if  person.food.food_love == i.name:
			i.get_node("OptionButton").selected = 1
		elif person.food.food_hate.has(i.name):
			i.get_node("OptionButton").selected = 2
		else:
			i.get_node("OptionButton").selected = 0

	finish_diet_selection()

#	$DietPanel/Button.disabled = !(person.get_stat('food_love') != '' && person.get_stat('food_hate').size() > 0)


func finish_diet_selection():
	preservedsettings['food_love'] = person.food.food_love
	valid_preservedsettings.food_love = true
	preservedsettings['food_hate'] = person.food.food_hate
	valid_preservedsettings.food_hate = true
	var text = person.food.food_love + "|"
	for i in person.food.food_hate:
		text += i + " "
#	$bodyparts2/diet.text = text

func open(type = 'slave', newguild = 'none', is_from_cheats = false):
	preservedsettings.clear()
	valid_preservedsettings.clear()
	build_class()
	build_race()
	build_sex_trait()
	build_trait()
	show()
	guild = newguild
#	$CancelButton.visible = input_handler.CurrentScreen == 'mansion'
	$introduction.bbcode_text = introduction_text[type]
	if type == 'slave':
		$introduction.bbcode_text += " " + str(ResourceScripts.game_party.characters.size())
	selected_class = ''

	person = ResourceScripts.scriptdict.class_slave.new("char_creation")
	mode = type
	person.set_stat('age', 'adult')
	person.set_stat('race', 'Human')
	match mode:
		'master':
			person.set_stat('sex', 'male')
			total_stat_points = variables.master_starting_stats
		'slave':
			person.set_stat('sex', 'female')
			total_stat_points = variables.slave_starting_stats

#	$bodyparts2/type_label.visible = mode == 'slave'
#	$bodyparts2/slave_class.visible = mode == 'slave'
#	$bodyparts2/slave_class.select(0)
	globals.connecttexttooltip($SlaveCreationModule/ScrollContainer/HBoxContainer/bodyparts2/slave_class_label, "Slave&Peon:\n" + tr('SLAVECLASSDESCRIPT') + "\n\n" + tr('SERVANTCLASSDESCRIPT'))
	$BackButton.visible = type != 'slave' || is_from_cheats
	$BackButtonCheats.visible = is_from_cheats
	rebuild_slave()


func rebuild_slave():
	SlaveInfo.build_bodyparts()
	var race = person.get_stat('race')
	var sex = person.get_stat('sex')
	var age = person.get_stat('age')
	var t_person = ResourceScripts.scriptdict.class_slave.new("char_creation_rebuild")
	t_person.create(race, sex, age)
	t_person.is_active = false
	t_person.is_known_to_player = true
	if mode == 'master':
		t_person.unlock_class('master')
	$VBoxContainer/HBoxContainer/SexVBox/sex.select(sexarray.find(sex))
	$VBoxContainer/HBoxContainer/AgeVBox/age.select(agearray.find(age))
	build_class()
	build_race()
	build_sex_trait()
	build_trait()
	if preservedsettings.has("food_love") and valid_preservedsettings.food_love:
		if preservedsettings.food_love != '':
			t_person.food.food_love = preservedsettings["food_love"]
	else:
		t_person.set_stat('food_love',  '')
	if preservedsettings.has("food_hate") and valid_preservedsettings.food_hate:
		if !preservedsettings.food_hate.empty():
			t_person.food.food_hate = preservedsettings["food_hate"]
	else:
		t_person.set_stat('food_hate', [])
	for i in ['name','surname','nickname']:
		if preservedsettings.has(i) and valid_preservedsettings[i]:
			$VBoxContainer.get_node(i).text = preservedsettings[i]
		else:
			$VBoxContainer.get_node(i).text = t_person.get_stat(i)
	
	person = t_person
	select_diet()
	if preservedsettings.has("sex_traits") && preservedsettings.sex_traits != null:
		select_sex_trait(preservedsettings.sex_traits)
	
	RebuildStatsContainer()
	SlaveInfo.build_bodyparts()
	SlaveInfo.get_node("descript").bbcode_text = ResourceScripts.descriptions.trim_tag(person.make_description(), 'url', 'hair')


func delete_keys_from_preservedsettings(keys):
	for key in keys:
		preservedsettings.erase(key)
		valid_preservedsettings[key] = false


func apply_preserved_settings():
	var racedata = races.racelist[person.get_stat('race')].bodyparts
	var keys_to_delete = []
	for i in bodypartsarray:
		if !races.racelist.Human.bodyparts.has(i) && preservedsettings.has(i) && preservedsettings[i] != '' && !racedata.has(i):
			keys_to_delete.append(i)
	delete_keys_from_preservedsettings(keys_to_delete)
	for i in preservedsettings:
		if !valid_preservedsettings.has(i): continue
		if !valid_preservedsettings[i]: continue
		if i == "food_love":
			person.food.food_love = preservedsettings["food_love"]
		elif i == "food_hate":
			person.food.food_hate = preservedsettings["food_hate"]
		elif i == 'slave_class':
			person.set_slave_category(preservedsettings[i])
		else:
			person.set_stat(i, preservedsettings[i])


func confirm_character():
	if check_confirm_possibility():
		if mode == 'master' && person.get_stat('sex') == 'female':
			confirm_female()
		else:
			confirm_final()

func confirm_female():
	input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'confirm_final', tr('CREATECHARACTERFEMALE')])

func confirm_final():
	input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'finish_character', tr('CREATECHARQUESTION')])


func finish_character():
	apply_preserved_settings()
#	$TraitSelection.hide()
#	$ClassPanel.hide()
	person.is_active = true
	if preservedsettings.has("sex_traits") && preservedsettings.sex_traits != null:
		person.create_s_trait_select(preservedsettings.sex_traits)
	if preservedsettings.has("traits") && preservedsettings.traits != null:
		person.add_trait(preservedsettings.traits)
	person.unlock_class(selected_class)
	person.set_stat('food_consumption', 3)
	person.hp = person.get_stat('hpmax')
	person.mp = person.get_stat('mpmax')
	person.food.create() #rebuild food filter
	#i don't like handle starting eqipment here. but this is the only point where newly created characters are accessible - and we need to do this for the characters created during prologue
	globals.equip_char(person, 'club', {WeaponMace = 'wood'})
	if mode != 'master':
		if !preservedsettings.has('slave_class'):
			preservedsettings.slave_class = 'slave'
		person.set_slave_category(preservedsettings.slave_class.to_lower())
		person.set_stat('obedience', 48)
		person.set_stat('growth_factor', 5)
		if guild == 'fighters':
			person.add_trait('loyalty_combatant')
		if guild in ['servants', 'workers']:
			person.add_trait('loyalty_dress_work')
	else:
		person.set_slave_category('master')
		person.set_stat('consent', 1000)
		globals.equip_char(person, 'chest_base_cloth', {ArmorBaseCloth = 'cloth', ArmorTrim = 'wood'})
		globals.equip_char(person, 'legs_base_cloth', {ArmorBaseCloth = 'cloth', ArmorTrim = 'wood'})
	ResourceScripts.game_party.add_slave(person)
	
	self.hide()
	input_handler.emit_signal("CharacterCreated")
	input_handler.add_random_chat_message(person, 'hire')

func text_changed(text, value):
	# if text != '':
	preservedsettings[value] = text
	valid_preservedsettings[value] = true
	apply_preserved_settings()
	SlaveInfo.get_node("descript").bbcode_text = ResourceScripts.descriptions.trim_tag(person.make_description(), 'url', 'hair')


func check_confirm_possibility():
	if !preservedsettings.has('food_love') or !valid_preservedsettings.food_love:
		input_handler.SystemMessage("You must select a liked food type")
		return false
	if preservedsettings['food_love'] == '' or preservedsettings['food_hate'].empty() or !valid_preservedsettings.food_hate:
		input_handler.SystemMessage("You must select one liked and at least one hated food type.")
		return false

	if selected_class == '':
		input_handler.SystemMessage("You must select a starting Class")
		return false

	return true

func set_savefilename(text):
	savefilename = text + ".ch"

func hideSaveLoadPanel():
	ResourceScripts.core_animations.FadeAnimation($SaveLoadCharPanel, 0.5)
	yield(get_tree().create_timer(0.5), "timeout")
	$SaveLoadCharPanel.hide()
	# $SaveLoadCharPanel/SaveLoadButton.disconnect("pressed", self, "PressSaveCharacter")
	# $SaveLoadCharPanel/SaveLoadButton.disconnect("pressed", self, "PressLoadCharacter")
	savefilename = null
	saveloadstate = null

func SaveLoadCharPanel(saveloadmode):
	if saveloadmode == "save":
		$SaveLoadCharPanel/SaveLoadButton.text = "Save"
	else:
		$SaveLoadCharPanel/SaveLoadButton.text = "Load"
	$SaveLoadCharPanel.show()
	ResourceScripts.core_animations.UnfadeAnimation($SaveLoadCharPanel, 0.5)
	yield(get_tree().create_timer(0.5), "timeout")
	saveloadstate = saveloadmode
	$SaveLoadCharPanel/RichTextLabel.bbcode_text = tr('SAVETEMPLATEDESCRIPT')
	$SaveLoadCharPanel/LineEdit.clear()
	input_handler.ClearContainerForced($SaveLoadCharPanel/ScrollContainer/VBoxContainer)
	# if saveloadmode == "save":
	for i in input_handler.dir_contents(variables.userfolder + 'savedcharacters'):
		var savename = i.replace(variables.userfolder + 'savedcharacters/',"").replace('.ch', '')
		var newbutton = input_handler.DuplicateContainerTemplate($SaveLoadCharPanel/ScrollContainer/VBoxContainer)

		newbutton.get_node("Delete").connect("pressed", self, 'PressDeleteCharacter', [savename])
		newbutton.get_node("Label").text = savename
		newbutton.connect('pressed', self, 'PressSaveLoadCharacter', [savename])


func PressSaveLoadCharacter(savename):
	if saveloadstate == "save":
		PressSaveCharacter(savename)
	else:
		PressLoadCharacter(savename)


func PressSaveCharacter(savename = null):
	if savename != null:
		$SaveLoadCharPanel/LineEdit.text = savename
		savefilename = savename + ".ch"
	if savefilename == '.ch' or savefilename == '':
		return

	var file = File.new()
	if file.file_exists(variables.userfolder + 'savedcharacters/' + savefilename):
		input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'SaveCharacter', tr("OVERWRITETEMPLATECONFIRM")])
	else:
		SaveCharacter()


func SaveCharacter():
	apply_preserved_settings()
	var character_to_save = {}
	for i in params_to_save:
		character_to_save[i] = person.get_stat(i)
		if preservedsettings.has(i) and valid_preservedsettings[i] and !i in ["professions", "food_love", "food_hate"]: # && !preservedsettings in except_array:
			character_to_save[i] = preservedsettings[i]
	character_to_save.professions = selected_class
	character_to_save["tits_size"] = person.get_stat("tits_size")
	character_to_save["ass_size"] = person.get_stat("ass_size")
	character_to_save["balls_size"] = person.get_stat("balls_size")
	character_to_save.type = str(mode)
	var file = File.new()
	file.open(variables.userfolder + 'savedcharacters/' + savefilename, file.WRITE)

	var text = JSON.print(character_to_save)
	file.store_string(text)
	file.close()
	input_handler.SystemMessage("Character Template Saved")
	hideSaveLoadPanel()



func PressDeleteCharacter(savename):
	$SaveLoadCharPanel/LineEdit.text = savename
	input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'DeleteCharacter', tr("DELETETEMPLATECONFIRM")])

func PressLoadCharacter(savename = null):
	if savename == null:
		hideSaveLoadPanel()
	else:
		$SaveLoadCharPanel/LineEdit.text = savename
		input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'LoadCharacter', tr("LOADTEMPLATECONFIRM")])

var temp_character
func reset_profession():
	temp_character.professions = ''
	LoadCharacter(temp_character)


func LoadCharacter(updated_char_to_load = null):
	var character_to_load
	if updated_char_to_load == null:
		var loadfilename = $SaveLoadCharPanel/LineEdit.text + ".ch"
		var file = File.new()
		file.open(variables.userfolder + "savedcharacters/" + loadfilename, file.READ)
		var text = file.get_as_text()
		var parse_result
		parse_result = JSON.parse(text)
		character_to_load = parse_result.result
	else:
		character_to_load = updated_char_to_load
	temp_character = character_to_load
	# Load availability checking
	if character_to_load != null && character_to_load.type != mode:
		input_handler.get_spec_node(input_handler.NODE_CONFIRMPANEL, [self, 'hideSaveLoadPanel', tr("TEMPLATETYPENOTMATCH")])
		return
	if guild != "none":
		if character_to_load != null && character_to_load.professions != '' && !character_to_load.professions in variables.get(guild+'_starting_classes'):
			input_handler.get_spec_node(input_handler.NODE_CONFIRMPANEL, [self, 'reset_profession', tr("RESETLOADCHARPROFESSION")])
			return
	var check_stats = 0
	var stats_array = []
	for i in character_to_load:
		if i.ends_with("_factor") && !i in ['growth_factor', 'timid_factor', 'tame_factor']:
			check_stats += character_to_load[i]
			stats_array.append(i)
	if check_stats > 15:
		for i in stats_array:
			character_to_load[i] = 1
	selected_class = character_to_load.professions
	build_class()
	for i in character_to_load:
		if !(i in params_to_save): continue
		if i == "food_love":
			person.food.food_love = character_to_load["food_love"]
			preservedsettings[i] = character_to_load["food_love"]
			valid_preservedsettings[i] = true
			continue
		elif i == "food_hate":
			person.food.food_hate = character_to_load["food_hate"]
			preservedsettings[i] = character_to_load["food_hate"]
			valid_preservedsettings[i] = true
			continue
		elif i == 'sex_traits':
			preservedsettings.erase(i)
			valid_preservedsettings[i] = false
			if Traitdata.sex_traits.has(character_to_load.sex_traits):
				preservedsettings.sex_traits = character_to_load.sex_traits
				valid_preservedsettings[i] = true
			continue
		person.set_stat(i, character_to_load[i])
		preservedsettings[i] = character_to_load[i]
		valid_preservedsettings[i] = true

	for i in ['name','surname','nickname']:
		if preservedsettings.has(i) and valid_preservedsettings[i]:
#			valid_preservedsettings[i] = false
			$VBoxContainer.get_node(i).text = preservedsettings[i]
		else:
			$VBoxContainer.get_node(i).text = person.get_stat(i)
	# apply_preserved_settings()
	SlaveInfo.build_bodyparts()
	SlaveInfo.get_node("descript").bbcode_text = ResourceScripts.descriptions.trim_tag(person.make_description(), 'url', 'hair')
	# if person.statlist.sex_traits.size() == 0:
	# 	$VBoxContainer/sextrait.text = "Select Sex Trait"
	# else:
	# 	$VBoxContainer/sextrait.text = Traitdata.sex_traits[person.statlist.sex_traits[0]].name
	for node in get_children():
		if node.get_child_count() != 0:
			for i in node.get_children():
				if i.get_name() in person:
					if i.get_class() == "CheckBox":
						i.pressed = person[i.get_name()]
					elif i.get_class() == "OptionButton":
						continue
						# for i in $DietPanel/VBoxContainer.get_children():
						# 	i.get_node("OptionButton").connect("item_selected", self, "select_food_pref", [i.name])
					else:
						i.text = person[i.get_name()].capitalize()
	RebuildStatsContainer()
	rebuild_slave()
	select_diet()
	finish_diet_selection()
	hideSaveLoadPanel()
	input_handler.SystemMessage("Character Template Loaded")
	# person.create_s_trait_select(character_to_load.sex_traits)
	temp_character = null


func DeleteCharacter():
	var deletename = $SaveLoadCharPanel/LineEdit.text + ".ch"
	var dir = Directory.new()
	dir.remove(variables.userfolder + 'savedcharacters/' + deletename)
	input_handler.SystemMessage("Character Deleted")
	SaveLoadCharPanel(saveloadstate)



func RebuildStatsContainer():
	input_handler.ClearContainer($StatsModule/StatsContainer)
	var array = []
	for i in statdata.statdata.values():
		if i.has('type') && i.type == 'factor':
			array.append(i)
			if preservedsettings.has(i.code) == false or !valid_preservedsettings[i.code]:
				preservedsettings[i.code] = 1
				valid_preservedsettings[i.code] = true
			else:
				person.set_stat(i.code, preservedsettings[i.code])
			if i.code in ['growth_factor','timid_factor','tame_factor'] && mode == 'master':
				preservedsettings[i.code] = 5
				valid_preservedsettings[i.code] = true
	
	var counter = total_stat_points
	
	build_sex_trait()
	for i in array:
		if mode == 'master' && i.code in ["growth_factor",'timid_factor','tame_factor']:
			continue
		if mode != 'master' && i.code in ["growth_factor"]:
			continue
		var newnode = input_handler.DuplicateContainerTemplate($StatsModule/StatsContainer)
		newnode.get_node("up").connect("pressed", self, 'stat_up', [i])
		newnode.get_node("down").connect("pressed", self, 'stat_down', [i])
		newnode.get_node("Label").text = str(preservedsettings[i.code])
		counter -= preservedsettings[i.code]-1
		newnode.get_node("icon").texture = i.baseicon
		var text = i.descript
		if i.code in ['physics_factor','wits_factor','charm_factor','sexuals_factor']:
			text += '\n\n' + statdata.statdata[i.code.replace('_factor', '')].descript
		globals.connecttexttooltip(newnode.get_node("icon"), text)

	unassigned_points = counter
	$StatsModule/totalstatlabel.text = 'Free points left: ' + str(counter)

	apply_preserved_settings()
	if selected_class != '' && !person.checkreqs(classesdata.professions[selected_class].reqs):
		selected_class = ''
		build_class()

func stat_up(stat):
	if preservedsettings[stat.code] >= 6 || unassigned_points == 0:
		return
	else:
		preservedsettings[stat.code] += 1
		valid_preservedsettings[stat.code] = true
		RebuildStatsContainer()
		$ClassSelectionModule.update_class_buttons()

func stat_down(stat):
	if preservedsettings[stat.code] <= 1:
		return
	else:
		preservedsettings[stat.code] -= 1
		valid_preservedsettings[stat.code] = true
		RebuildStatsContainer()
		$ClassSelectionModule.update_class_buttons()

func open_sex_traits():
	hide_all_dialogues()
	TraitSelection.build_sex()


func open_traits():
	hide_all_dialogues()
	TraitSelection.build_trait()


func select_sex_trait(trait_id):
#	person.create_s_trait_select(trait_id)
	preservedsettings["sex_traits"] = trait_id
	valid_preservedsettings["sex_traits"] = true
	$TraitSelection.hide()
	input_handler.get_spec_node(input_handler.NODE_TEXTTOOLTIP).hide()
	RebuildStatsContainer()
	build_sex_trait()


func select_trait(trait_id):
#	person.add_trait(trait_id)
	preservedsettings["traits"] = trait_id
	valid_preservedsettings["traits"] = true
	$TraitSelection.hide()
	input_handler.get_spec_node(input_handler.NODE_TEXTTOOLTIP).hide()
	RebuildStatsContainer()
	build_trait()


func build_trait():
	if preservedsettings.has("traits") && preservedsettings.traits != null:
		var trdata = Traitdata.traits[preservedsettings.traits]
		$VBoxContainer/trait/Label.text = tr(trdata.name)
		if trdata.has('icon') and trdata.icon != null:
			if trdata.icon is String:
				$VBoxContainer/trait/icon.texture = load(trdata.icon)
			else:
				$VBoxContainer/trait/icon.texture = trdata.icon
			if trdata.tags.has('simple_icon'):
				$VBoxContainer/trait/bg.texture = null
			else:
				if trdata.tags.has('positive'):
					$VBoxContainer/trait/bg.texture = load("res://assets/images/iconstraits/green.png")
				elif trdata.tags.has('negative'):
					$VBoxContainer/trait/bg.texture = load("res://assets/images/iconstraits/red.png")
				else:
					$VBoxContainer/trait/bg.texture = load("res://assets/images/iconstraits/grey.png")
		else:
			$VBoxContainer/trait/bg.texture = null
			$VBoxContainer/trait/icon.texture = null
	else:
		$VBoxContainer/trait/bg.texture = null
		$VBoxContainer/trait/icon.texture = null
		$VBoxContainer/trait/Label.text = "Trait"


func build_sex_trait():
	if preservedsettings.has("sex_traits") && preservedsettings.sex_traits != null:
		var trdata = Traitdata.sex_traits[preservedsettings.sex_traits]
		$VBoxContainer/sextrait/Label.text = tr(trdata.name)
	else:
		$VBoxContainer/sextrait/Label.text = "Sex Trait"


func build_race():
	if preservedsettings.has("race") && preservedsettings.race != null:
		var rdata = races.racelist[preservedsettings.race]
		$VBoxContainer/race/Label.text = tr(rdata.name)
		if rdata.icon is String:
			$VBoxContainer/race/icon.texture = load(rdata.icon)
		else:
			$VBoxContainer/race/icon.texture = rdata.icon
	else:
		if !preservedsettings.empty():
			print('warn - race selection not valid')
		$VBoxContainer/race/Label.text = "Race"
		$VBoxContainer/race/icon.texture = null


func build_class():
	if selected_class != "":
		var trdata = classesdata.professions[selected_class]
		$VBoxContainer/class/Label.text = tr(trdata.name)
		if trdata.has('icon') and trdata.icon != null:
			if trdata.icon is String:
				$VBoxContainer/class/icon.texture = load(trdata.icon)
			else:
				$VBoxContainer/class/icon.texture = trdata.icon
		else:
			$VBoxContainer/class/icon.texture = null
	else:
		$VBoxContainer/class/icon.texture = null
		$VBoxContainer/class/Label.text = "Class"



func confirm_return():
	input_handler.get_spec_node(input_handler.NODE_YESNOPANEL, [self, 'cancel_creation', tr("RETURNTOMAINMENUQUESTION")])
	#input_handler.ShowConfirmPanel(self, "cancel_creation", "Return to Main Menu?")


func test():
#	print("Preserved Settings:", preservedsettings)
#	print(person.get_stat("sex"))
#	print(person.get_stat("age"))
#	print(person.get_stat("race"))
#	print(person.get_full_name())
	print("Sex Traits:", person.statlist.sex_traits)


func hide_all_dialogues():
	$SaveLoadCharPanel.hide()
	RaceSelection.hide()
	TraitSelection.hide()
	ClassSelection.hide()
